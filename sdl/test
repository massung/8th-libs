\ -*- 8th -*-

ns: sdl-test

\ This is just an example usage of the SDL library and a quick test to
\ make sure the basics are working properly.

needs sdl/sdl
with: sdl

var window
var renderer

: create-window \ --
    "SDL Test" SDL_WINDOWPOS_UNDEFINED dup 200 dup SDL_WINDOW_SHOWN

    \ attempt to create the window, die on failure
    SDL_CreateWindow "Failed to create SDL window!" thrownull

    \ save the pointer
    window ! ;

: create-renderer \ --
    window @ -1 SDL_RENDERER_ACCELERATED

    \ attempt to create the renderer, die on failure
    SDL_CreateRenderer "Failed to create SDL renderer!" thrownull

    \ save the pointer
    renderer ! ;

: draw \ --
    renderer @ dup

    \ clear the viewport and swap buffers
    SDL_RenderClear drop
    SDL_RenderPresent ;

: maybe-quit \ b --
    SDL_KeyboardEvent:unpack drop
    SDL_KeyboardEvent:keysym

    SDL_Keysym:unpack drop
    SDL_Keysym:sym

    SDLK_ESCAPE n:= if
        bye
    then ;

: process-event \ b n --
    [ ( dup SDL_KEYDOWN n:= ) , ( drop maybe-quit )
    , ' 2drop
    ] when ;

: process-events \ --
    poll-event null; process-event process-events ;

: run-test \ --
    sdl-init

    \ initialization of globals
    create-window
    create-renderer

    \ run loop
    repeat
        process-events
        draw
    again ;

\ done with SDL
;with

: app:main \ --
    run-test ;
