\ -*- 8th -*-

needs ext/rec

ns: glib

\ mapping of key/button constants to action words
m:new var, key-bindings
m:new var, mouse-bindings

\ define a map record for an action binding
rec: action
    field: state
    field: hits
rec;

-- w: glib:action:
-- s: <name> -- \\\ IMMEDIATE
-- d: Define a new action that can be bound to a key or mouse button.

: action: \ <name> -- \\\ IMMEDIATE
    0 0 action:new p: var, i;

-- w: glib:bind-key
-- s: n action --
-- d: Bind the keycode n to the action.

: bind-key \ n action --
    key-bindings @ -rot m:! drop ;

-- w: glib:bind-mouse
-- s: n action --
-- d: Bind a mouse button n to the action.

: bind-mouse \ n action --
    mouse-bindings @ -rot m:! drop ;

-- w: glib:released
-- s: action -- T
-- d: True if the action is currently in the released state.

: released \ action -- T
    @ action:state nip %1 n:band 0 = ;

-- w: glib:just-released
-- s: action -- T
-- d: True if the action was just released.

: just-released \ action -- T
    @ action:state nip %10 = ;

-- w: glib:pressed
-- s: action -- T
-- d: True if the action is currently in the pressed state.

: pressed \ action -- T
    @ action:state nip %1 n:band 1 = ;

-- w: glib:just-pressed
-- s: action -- T
-- d: True if the action was just pressed.

: just-pressed \ action -- T
    @ action:state nip %11 = ;

-- w: glib:clear-hits
-- s: action --
-- d: Remove all hits from an action.

: clear-hits \ action --
    @ 0 action:hits! drop ;

-- w: glib:release
-- s: action --
-- d: Update the action to the released state.

: release \ action --
    @ action:state 1 shl %11 band action:state! drop ;

-- w: glib:press
-- s: action --
-- d: Update the action to the pressed state.

: press \ action --
    @ action:state 1 bxor 1 shl 1 bor %11 band action:state! drop ;

-- w: glib:on-keydown
-- s: b --
-- d: Update any bound actions.

: on-keydown \ b --
    SDL_KeyboardEvent:unpack drop
    SDL_KeyboardEvent:keysym
    SDL_Keysym:unpack drop
    SDL_Keysym:sym nip

    \ lookup the binding for this keycode and press it
    key-bindings @ swap m:_@ null; press ;

-- w: glib:on-keyup
-- s: b --
-- d: Update any bound actions.

: on-keyup \ b --
    SDL_KeyboardEvent:unpack drop
    SDL_KeyboardEvent:keysym
    SDL_Keysym:unpack drop
    SDL_Keysym:sym nip

    \ lookup the binding for this keycode and release it
    key-bindings @ swap m:_@ null; release ;

-- w: glib:on-mousedown
-- s: b --
-- d: Update any bound actions.

: on-mousedown \ b --
    SDL_MouseButtonEvent:unpack drop
    SDL_MouseButtonEvent:button

    \ lookup the binding for this button and press it
    mouse-bindings @ swap m:_@ null; press ;

-- w: glib:on-mousei[
-- s: b --
-- d: Update any bound actions.

: on-mouseup \ b --
    SDL_MouseButtonEvent:unpack drop
    SDL_MouseButtonEvent:button

    \ lookup the binding for this button and release it
    mouse-bindings @ swap m:_@ null; release ;
